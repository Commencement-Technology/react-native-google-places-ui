// Top-level build file where you can add configuration options common to all sub-projects/modules.
import groovy.json.JsonSlurper

// dynamically retrieve version from package.json
def computePackageVersion() {
    def slurper = new JsonSlurper()
    def json = slurper.parse(file('../package.json'), 'utf-8')
    return json.version
}

// Este block encapsula propiedades personalizadas y las pone a disposición
// de todos los módulos del proyecto.
// https://developer.android.com/studio/build/gradle-tips.html
ext {
    googlePlayServicesVersion = '15.0.1'
    minSdkVersion      = 21
    compileSdkVersion  = 27
    targetSdkVersion   = 27
    supportLibVersion  = '27.1.1'
    buildToolsVersion  = '27.0.3'
    packageJsonVersion = computePackageVersion()
}

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        jcenter()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
            name 'React Native (local)'
        }
    }
}

configurations.all {
    resolutionStrategy {
        force "com.android.support:appcompat-v7:$supportLibVersion"
        force "com.android.support:support-v4:$supportLibVersion"
    }
}

/*
    Forces all the submodules to use the specified compileSdkVersion & buildToolsVersion.
    https://github.com/oblador/react-native-keychain/issues/68#issuecomment-304836725
*/
subprojects {
    afterEvaluate {project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion rootProject.compileSdkVersion
                buildToolsVersion rootProject.buildToolsVersion
            }
        }
    }
}
